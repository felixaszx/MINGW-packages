# Maintainer: Sarah Ottinger <schalaalexiazeal@gmail.com>
# Update to 4.3-stable and modified by: Felix Xing (Felixaszx) felixxing3009@outlook.com
# Base on Arch Linux: https://gitlab.archlinux.org/archlinux/packaging/packages/godot/

_realname=godot
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=4.3
pkgrel=1
pkgdesc='An advanced, feature packed, multi-platform 2D and 3D game engine (mingw-w64)'
url='https://godotengine.org/'
msys2_references=(
  "cpe: cpe:/a:godotengine:godot"
  "cpe: cpe:/a:godotengine:godot_engine"
)
license=('spdx:MIT')
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64')
makedepends=("make"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-scons")
depends=()
optdepends=()
source=("https://github.com/godotengine/godot/releases/download/${pkgver}-stable/${_realname}-${pkgver}-stable.tar.xz")
sha256sums=('751e55bfad8e04b846f9cf7b6eb80e058986a2cb1b103fc0fe6a4d8526a20e56')

prepare() {
  cd ${_realname}-${pkgver}-stable

  patch -p1 -i "../../001-fix-llvm-duplicate-symbol.patch"
}

build() {
  cd ${_realname}-${pkgver}-stable

  declare -a extra_config
  if [[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]]; then
    extra_config+=("use_llvm=yes")
  fi

  scons \
    platform=windows \
    arch=x86_64\
    target=editor \
    optimize=speed\
    production=yes\
    use_mingw=yes \
    "${extra_config[@]}"
}

package() {
  cd ${_realname}-${pkgver}-stable
  if [[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]]; then
    install -Dm755 bin/godot.windows.editor.x86_64.llvm.exe "${pkgdir}${MINGW_PREFIX}/bin/godot.exe"
  else
    install -Dm755 bin/godot.windows.editor.x86_64.exe "${pkgdir}${MINGW_PREFIX}/bin/godot.exe"
  fi
  install -Dm644 icon.svg "${pkgdir}${MINGW_PREFIX}/share/pixmaps/godot.svg"
  install -D -m644 LICENSE.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/godot/LICENSE.txt"
  install -D -m644 "misc/dist/linux/godot.6" "${pkgdir}${MINGW_PREFIX}/share/man/man6/godot.6"
}

